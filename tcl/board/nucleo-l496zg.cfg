# This is an nucleo-l496zg-p with a single STM32L496ZGTP chip.
# http://www.st.com/en/evaluation-tools/nucleo-l496zg-p.html
#
# Two Winbond W25Q128FV attached, dual-1-line or dual-4-line mode
#

# This is for using the onboard STLINK
source [find interface/stlink.cfg]

transport select hla_swd

# increase working area to 64KB
set WORKAREASIZE 0x10000

# enable stmqspi
if {![info exists QUADSPI]} {
	set QUADSPI 1
}

source [find target/stm32l4x.cfg]

#reset_config srst_only srst_nogate connect_assert_srst

# QUADSPI initialization
# flash = 1: only flash1, = 2: only flash 2, anything else: both flash1 and flash2
# qpi: 4-line mode
proc qspi_init { flash qpi } {
	mmw 0x4002104C 0x000001FF 0				;# RCC_AHB2ENR |= GPIOA-GPIOI (enable clocks)
	mmw 0x40021050 0x00000100 0				;# RCC_AHB3ENR |= QSPIEN (enable clock)
	sleep 1									;# Wait for clock startup

	# PA02: BK1_NCS, PE10: CLK, PE15: BK1_IO3, PE14: BK1_IO2, PE13: BK1_IO1, PE12: BK1_IO0
	# PE10: CLK, PC04: BK2_IO3, PC03: BK2_IO2, PC02: BK2_IO1, PC01: BK2_IO0

	# PA02:AF10:V, PC04:AF10:V, PC03:AF10:V, PC02:AF10:V, PC01:AF10:V, PE15:AF10:V
	# PE14:AF10:V, PE13:AF10:V, PE12:AF10:V, PE10:AF10:V

	# Port A: PA02:AF10:V
	mmw 0x48000000 0x00000020 0x00000010    ;# MODER
	mmw 0x48000008 0x00000030 0x00000000    ;# OSPEEDR
	mmw 0x48000020 0x00000A00 0x00000500    ;# AFRL

	# Port C: PC04:AF10:V, PC03:AF10:V, PC02:AF10:V, PC01:AF10:V
	mmw 0x48000800 0x000002A8 0x00000154    ;# MODER
	mmw 0x48000808 0x000003FC 0x00000000    ;# OSPEEDR
	mmw 0x48000820 0x000AAAA0 0x00055550    ;# AFRL

	# Port E: PE15:AF10:V, PE14:AF10:V, PE13:AF10:V, PE12:AF10:V, PE10:AF10:V
	mmw 0x48001000 0xAA200000 0x55100000    ;# MODER
	mmw 0x48001008 0xFF300000 0x00000000    ;# OSPEEDR
	mmw 0x48001024 0xAAAA0A00 0x55550500    ;# AFRH

	if { $flash == 1 } {
		# for flash 1 only w25q128fv
		mww 0xA0001000 0x01500018			;# QUADSPI_CR: PRESCALER=1, APMS=1, FTHRES=0, FSEL=0, DFM=0, SSHIFT=1, TCEN=1
		mww 0xA0001004 0x00170100			;# QUADSPI_DCR: FSIZE=0x17, CSHT=0x01, CKMODE=0
	} else {
		if { $flash == 2 } {
			# for flash 2 only w25q128fv
			mww 0xA0001000 0x01500098		;# QUADSPI_CR: PRESCALER=1, APMS=1, FTHRES=0, FSEL=1, DFM=0, SSHIFT=1, TCEN=1
			mww 0xA0001004 0x00170100		;# QUADSPI_DCR: FSIZE=0x17, CSHT=0x01, CKMODE=0
		} else {
			# for dual flash mode 2 * w25q128fv
			mww 0xA0001000 0x01500058		;# QUADSPI_CR: PRESCALER=1, APMS=1, FTHRES=0, FSEL=0, DFM=1, SSHIFT=1, TCEN=1
			mww 0xA0001004 0x00180100		;# QUADSPI_DCR: FSIZE=0x18, CSHT=0x01, CKMODE=0
		}
	}

	mww 0xA0001014 0x0D002503				;# QUADSPI_CCR: FMODE=0x3, DMODE=0x1, DCYC=0x0, ADSIZE=0x3, ADMODE=0x1, IMODE=0x1
	mmw 0xA0001000 0x00000001 0				;# QUADSPI_CR: EN=1

	if { $qpi == 1 } {
		# Enter QPI mode
		mww 0xA0001014 0x00000138			;# QUADSPI_CCR: FMODE=0x0, DMODE=0x0, DCYC=0x0, ADSIZE=0x0, ADMODE=0x0, IMODE=0x1, INSTR=Enter QPI
		sleep 1

		# memory-mapped fast read mode with 3-byte addresses and 2 dummy cycles (for read only)
		mww 0xA0001014 0x0F082F0B			;# QUADSPI_CCR: FMODE=0x3, DMODE=0x3, DCYC=0x2, ADSIZE=0x3, ADMODE=0x3, IMODE=0x3, INSTR=Fast READ

		# as QSPI is set to instruction/address/data on 4 lines in memory mapped mode, driver will *always*
		# use this setting (i. e. for probe, erase, write)
		# the 'Enter QPI mode' command is chip specific, additionally both w25q128fv must be configured in advance by
		# programming the *NON*-volatile QE bit (bit 1 in status register 2), e. g. by the following commands
		#
		# stmqspi cmd 1 0 0x06
		# stmqspi cmd 1 0 0x31 0x02 0x02
		# stmqspi cmd 1 2 0x35
		#
		# the last one should return  '-> 02 02' to indicate successful setting of QE bit
		# furthemore, the flash chip changes its id from 0x1940ef to 0x1960ef upon entering QPI mode
	} else {
		# memory-mapped read mode with 3-byte addresses
		mww 0xA0001014 0x0D002503			;# QUADSPI_CCR: FMODE=0x3, DMODE=0x1, DCYC=0x0, ADSIZE=0x3, ADMODE=0x1, IMODE=0x1, INSTR=READ
	}
}

$_TARGETNAME configure -event reset-init {
	global QUADSPI

	mmw 0x40022000 0x00000003 0x0000000C	;# 3 WS for 72 MHz HCLK
	sleep 1
	mmw 0x40021000 0x00000100 0x00000000	;# HSI on
	mww 0x4002100C 0x01002432				;# 72 MHz: PLLREN=1, PLLM=4, PLLN=36, PLLR=2, HSI
	mww 0x40021008 0x00008001				;# always HSI, APB1: /1, APB2: /1
	mmw 0x40021000 0x01000000 0x00000000	;# PLL on
	sleep 1
	mmw 0x40021008 0x00000003 0x00000000	;# switch to PLL
	sleep 1

	adapter_khz 4000

	if { $QUADSPI } {
		qspi_init 0 1
	}
}
