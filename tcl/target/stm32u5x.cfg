# SPDX-License-Identifier: GPL-2.0-or-later

# script for stm32u5x family

#
# stm32u5 devices support both JTAG and SWD transports.
#
source [find target/swj-dp.tcl]
source [find mem_helper.tcl]

if { [info exists CHIPNAME] } {
	set _CHIPNAME $CHIPNAME
} else {
	set _CHIPNAME stm32u5x
}

set _ENDIAN little

# Work-area is a space in RAM used for flash programming
# By default use 64kB at address 0x20000000
if { [info exists WORKAREASIZE] } {
	set _WORKAREASIZE $WORKAREASIZE
} else {
	set _WORKAREASIZE 0x10000
}

if { [info exists WORKAREAADDR] } {
	set _WORKAREAADDR $WORKAREAADDR
} else {
	set _WORKAREAADDR 0x20000000
}

# When RDP = 0x55, only a part of the RAM is set as non-secure by the secure
# application using the Secure Attribution Unit (SAU).

# This non-secure RAM cannot be auto-detected by the debugger, so to avoid
# programming issues, by default do not use the work-area and fall back to flash
# programming witouth flash loader (slower).
# If the user knows about the current SAU partitionning, he can provide
# the work-area address and size then force the usage of the loader by setting
# USE_LOADER_IN_RDP_05 to 1

# By default do not use the flash loader in RDP 0.5
if { [info exists USE_LOADER_IN_RDP_05] } {
	set _USE_LOADER_IN_RDP_05 $USE_LOADER_IN_RDP_05
} else {
	set _USE_LOADER_IN_RDP_05 0
}

#jtag scan chain
if { [info exists CPUTAPID] } {
	set _CPUTAPID $CPUTAPID
} else {
	if { [using_jtag] } {
		# See STM Document RM0438
		# RM0456 Rev1, Section 65.2.8 JTAG debug port - Table 661. JTAG-DP data registers
		# Corresponds to CortexÂ®-M33 JTAG debug port ID code
		set _CPUTAPID 0x0ba04477
	} {
		# SWD IDCODE (single drop, arm)
		set _CPUTAPID 0x0be12477
	}
}

swj_newdap $_CHIPNAME cpu -irlen 4 -ircapture 0x1 -irmask 0xf -expected-id $_CPUTAPID
dap create $_CHIPNAME.dap -chain-position $_CHIPNAME.cpu

if {[using_jtag]} {
	jtag newtap $_CHIPNAME bs -irlen 5
}

set _TARGETNAME $_CHIPNAME.cpu
target create $_TARGETNAME cortex_m -endian $_ENDIAN -dap $_CHIPNAME.dap

# use non-secure RAM by default
$_TARGETNAME configure -work-area-phys 0x20000000 -work-area-size $_WORKAREASIZE -work-area-backup 0

# create sec/ns flash and otp memories (sizes will be probed)
flash bank $_CHIPNAME.flash_ns      stm32l4x 0x08000000 0 0 0 $_TARGETNAME
flash bank $_CHIPNAME.flash_alias_s stm32l4x 0x0C000000 0 0 0 $_TARGETNAME
flash bank $_CHIPNAME.otp           stm32l4x 0x0BFA0000 0 0 0 $_TARGETNAME

# Common knowledges tells JTAG speed should be <= F_CPU/6.
# F_CPU after reset is MSI 4MHz, so use F_JTAG = 500 kHz to stay on
# the safe side.
#
# Note that there is a pretty wide band where things are
# more or less stable, see http://openocd.zylin.com/#/c/3366/
adapter speed 500

adapter srst delay 100
if {[using_jtag]} {
	jtag_ntrst_delay 100
}

reset_config srst_nogate

if {[using_hla]} {
	echo "Warn : The selected transport do not support debbuging this device in secure mode"
} else {
	# if srst is not fitted use SYSRESETREQ to
	# perform a soft reset
	cortex_m reset_config sysresetreq
}

proc stm32u5x_is_secure {} {
	# read Debug Security Control and Status Regsiter (DSCSR) and check CDS (bit 16)
	set DSCSR [mrw 0xE000EE08]
	set secure [expr {($DSCSR & (1 << 16)) != 0}]
	if {$secure && [using_hla]} {
		echo "Error: The selected transport do not support debbuging this device in secure mode"
		shutdown
	}

	return $secure
}

proc stm32u5x_clock_config_160_mhz {} {
	set offset [expr {[stm32u5x_is_secure] ? 0x10000000 : 0}]
	# MCU clock is at MSI 4MHz after reset, set MCU freq at 160 MHz with PLL

	# Enable voltage range 1 for frequency above 100 Mhz
	# RCC_AHB3ENR = PWREN
	mww [expr {0x46020C94 + $offset}] 0x00000004
	# delay for register clock enable (read back reg)
	mrw [expr {0x56020C94 + $offset}]
	# PWR_VOSR : VOS Range 1
	mww [expr {0x4602080C + $offset}] 0x00030000
	# delay for register write (read back reg)
	mrw [expr {0x4602080C + $offset}]
	# FLASH_ACR : 4 WS for 160 MHz HCLK
	mww [expr {0x40022000 + $offset}] 0x00000004
	# RCC_PLL1CFGR => PLL1M=0000=/1, PLL1SRC=MSI 4MHz
	mww [expr {0x46020C28 + $offset}] 0x00000001
	# RCC_PLL1DIVR => PLL1P=PLL1Q=PLL1R=000001=/2, PLL1N=0x4F=80
	# fVCO = 4 x 80 /1 = 320
	# SYSCLOCK = fVCO/PLL1R = 320/2 = 160 MHz
	mmw [expr {0x46020C34 + $offset}] 0x0000004F 0
	# RCC_PLL1CFGR => PLL1REN=1
	mmw [expr {0x46020C28 + $offset}] 0x00040000 0
	# RCC_CR |= PLL1ON
	mmw [expr {0x46020C00 + $offset}] 0x01000000 0
	# while !(RCC_CR & PLL1RDY)
	while {!([mrw [expr {0x46020C00 + $offset}]] & 0x02000000)} {}
	# RCC_CFGR1 |= SW_PLL
	mmw [expr {0x46020C1C + $offset}] 0x00000003 0
	# while ((RCC_CFGR1 & SWS) != PLL)
	while {([mrw [expr {0x46020C1C + $offset}]] & 0x0C) != 0x0C} {}
}

proc stm32u5x_ahb_ap_non_secure_access {} {
	# in HLA mode, non-secure debugging is possible without changing the AP CSW
	if {![using_hla]} {
		# SPROT=1=Non Secure access, Priv=1
		[[target current] cget -dap] apcsw 0x4B000000 0x4F000000
	}
}

proc stm32u5x_ahb_ap_secure_access {} {
	if {[using_hla]} {
		echo "Error: The selected transport do not support debbuging this device in secure mode"
		shutdown
	}
	# SPROT=0=Secure access, Priv=1
	[[target current] cget -dap] apcsw 0x0B000000 0x4F000000
}

proc stm32u5x_enter_debug {} {
	set _TARGETNAME [target current]
	# check security status
	set secure [stm32u5x_is_secure]

	# check flash options, from FLASH_OPTR register
	set optr [mrw 0x40022040]
	set tzen [expr {$optr & 0x80000000}]
	set rdp [expr {$optr & 0xFF}]

	if {$secure || $tzen} {
		stm32u5x_ahb_ap_secure_access
	} else {
		stm32u5x_ahb_ap_non_secure_access
	}

	# print the secure state only when it changes
	global $_TARGETNAME.secure
	set initialized [info exists $_TARGETNAME.secure]

	if {!$initialized || $secure != [set $_TARGETNAME.secure]} {
		# update saved security state
		set $_TARGETNAME.secure $secure

		set secure_str [expr {$secure ? "Secure" : "Non-Secure"}]
		echo "$_TARGETNAME in $secure_str state"
	}

	# avoid some noise, when reset is asserted OPTR is read as zero
	if {$optr == 0} { return }

	# ensure that workarea is correctly configured if there is a change in tzen or rdp
	global $_TARGETNAME.tzen $_TARGETNAME.rdp
	set initialized [expr {[info exists $_TARGETNAME.tzen] && [info exists $_TARGETNAME.rdp]}]

	if {!$initialized || $tzen != [set $_TARGETNAME.tzen] || $rdp != [set $_TARGETNAME.rdp]} {
		# update saved tzen and rdp state
		set $_TARGETNAME.tzen $tzen
		set $_TARGETNAME.rdp $rdp

		set tzen_state_str [expr {$tzen ? "enabled" : "disabled"}]
		echo "$_TARGETNAME TrustZone $tzen_state_str"

		# use secure workarea only when TZEN=1 and RDP!=0x55
		set use_secure_workarea 0
		if {$tzen && ($rdp != 0x55)} {
			set use_secure_workarea 1
		}

		set workarea_addr [$_TARGETNAME cget -work-area-phys]

		if {$use_secure_workarea} {
			set workarea_addr [expr {$workarea_addr | 0x10000000}]
		} else {
			set workarea_addr [expr {$workarea_addr & ~0x10000000}]
		}

		set workarea_addr_str [format "0x%08X" $workarea_addr]
		echo "$_TARGETNAME work-area address is set to $workarea_addr_str"
		$_TARGETNAME configure -work-area-phys $workarea_addr

		# when RDP=0x55 (TZEN=1), only non-secure flash could be programmed
		# but workarea is not accessible since the RAM is secure.
		# to fall back to programming without loader set the workarea size to zero

		global $_TARGETNAME.workarea_size
		if {![info exists $_TARGETNAME.workarea_size]} {
			set $_TARGETNAME.workarea_size [$_TARGETNAME cget -work-area-size]
		}

		if {$rdp == 0x55 && !$USE_LOADER_IN_RDP_05} {
			set workarea_size 0
			echo "$_TARGETNAME work-area is disabled"
		} else {
			set workarea_size [set $_TARGETNAME.workarea_size]
			echo "$_TARGETNAME work-area is enabled"
		}

		$_TARGETNAME configure -work-area-size $workarea_size
	}
}

$_TARGETNAME configure -event reset-init {
	stm32u5x_clock_config_160_mhz
	# Boost JTAG frequency
	adapter speed 4000
}

$_TARGETNAME configure -event reset-start {
	# Reset clock is MSI (4 MHz)
	adapter speed 480
}

$_TARGETNAME configure -event examine-end {
	stm32u5x_enter_debug

	# DBGMCU_CR |= DBG_STANDBY | DBG_STOP
	mmw 0xE0044004 0x00000006 0

	# Stop watchdog counters during halt
	# DBGMCU_APB1_FZ |= DBG_IWDG_STOP | DBG_WWDG_STOP
	mmw 0xE0044008 0x00001800 0
}

$_TARGETNAME configure -event halted {
	stm32u5x_enter_debug
}

$_TARGETNAME configure -event trace-config {
	# Set TRACE_IOEN; TRACE_MODE is set to async; when using sync
	# change this value accordingly to configure trace pins
	# assignment
	mmw 0xE0044004 0x00000020 0
}
